/*
 * DexPatcher - Copyright 2015-2018 Rodrigo Balerdi
 * (GNU General Public License version 3 or later)
 *
 * DexPatcher is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published
 * by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 */

/**
 * The buildscript block is where you configure the repositories and
 * dependencies for Gradle itselfâ€”meaning, you should not include dependencies
 * for your modules here. For example, this block includes the Android plugin for
 * Gradle as a dependency because it provides the additional instructions Gradle
 * needs to build Android app modules.
 */

buildscript {

    /**
     * The repositories block configures the repositories Gradle uses to
     * search or download the dependencies. Gradle pre-configures support for remote
     * repositories such as JCenter, Maven Central, and Ivy. You can also use local
     * repositories or define your own remote repositories. The code below defines
     * JCenter as the repository Gradle should use to look for its dependencies.
     *
     * New projects created using Android Studio 3.0 and higher also include
     * Google's Maven repository.
     */

    repositories {
        google()
        jcenter()
    }

    /**
     * The dependencies block configures the dependencies Gradle needs to use
     * to build your project. The following line adds Android plugin for Gradle
     * version 3.2.1 as a classpath dependency.
     */

    dependencies {
        // classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.9.10"
}

group = 'com.github.lanchon.dexpatcher'
version = '2.0.0-gradle46'

sourceCompatibility = '1.7'

repositories {
	google()
    jcenter()
}

dependencies {
    compile 'com.android.tools.build:gradle:3.2.1'
    compile gradleApi()
    compile localGroovy()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'DexPatcher-gradle',
                'Implementation-Version': version
        )
    }
}

afterEvaluate {
    def archivesArtifacts = configurations.archives.artifacts
    archivesArtifacts.removeAll archivesArtifacts.findAll { it.classifier }
}

pluginBundle {

    website = 'https://dexpatcher.github.io/'
    vcsUrl = 'https://github.com/DexPatcher/DexPatcher-gradle'

    description = "DexPatcher is a toolchain for modifying Android APK files at source-level using Java, " +
            "graphical resource editors, and the full power of Android Studio in all major platforms."

    tags = ['android', 'application', 'apk', 'dexpatcher', 'apktool', 'dex2jar', 'patch', 'modify', 'reverse']

    plugins {

        basePlugin {
            id = 'com.github.lanchon.dexpatcher.base'
            displayName = 'DexPatcher Base'
            description = "The DexPatcher Base plugin provides basic DexPatcher, Apktool and dex2jar tasks. " +
                    "DexPatcher is free software. (GPLv3+)"
        }

        apkLibraryPlugin {
            id = 'com.github.lanchon.dexpatcher.apk-library'
            displayName = 'DexPatcher APK Library'
            description = "The DexPatcher APK Library plugin processes an Android application (APK) file and produces " +
                    "a DexPatcher APK library file that can be used to create modified versions of the source application. " +
                    "The file is similar in structure to an Android library (AAR) file. It includes the decoded Android " +
                    "resources of the application and its Dalvik bytecode translated into Java bytecode among other things. " +
                    "DexPatcher is free software. (GPLv3+)"
        }

        patchedAppPlugin {
            id = 'com.github.lanchon.dexpatcher.patched-application'
            displayName = 'DexPatcher Patched Application'
            description = "The DexPatcher Patched Application plugin produces a modified Android application by applying " +
                    "changes and additions to code and resources of a source application imported as a DexPatcher APK library. " +
                    "DexPatcher is free software. (GPLv3+)"
        }

        patchLibraryPlugin {
            id = 'com.github.lanchon.dexpatcher.patch-library'
            displayName = 'DexPatcher Patch Library'
            description = "The DexPatcher Patch Library plugin produces a DexPatcher patch library file that bundles compiled " +
                    "changes and additions to code and resources of a source application imported as a DexPatcher APK library, " +
                    "without bundling any part of the source application itself. The file is similar in structure to an " +
                    "Android library (AAR) file. The changes bundled in a patch library can be applied to the source " +
                    "application using the DexPatcher Patched Application plugin. Several patch libraries can be applied " +
                    "in the same step, or their application can be serialized though the cyclic use of the APK Library " +
                    "and Patched Application plugins. DexPatcher is free software. (GPLv3+)"
        }

    }

    mavenCoordinates {
        groupId = 'com.github.lanchon.dexpatcher'
        artifactId = 'dexpatcher-gradle'
    }

}
